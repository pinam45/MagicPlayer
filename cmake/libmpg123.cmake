message(STATUS "Configuring libmpg123")

# Config
set(EMBEDDED_LIBMPG123_NAME                        "libmpg123")
set(EMBEDDED_LIBMPG123_VERSION                     "1.25.8")
set(EMBEDDED_LIBMPG123_MINGW_STATIC_FILENAME       "libmpg123.a")
set(EMBEDDED_LIBMPG123_MINGW_SHARED_FILENAME       "libmpg123-0.dll")
set(EMBEDDED_LIBMPG123_MSVC_STATIC_FILENAME        "libmpg123.lib")
set(EMBEDDED_LIBMPG123_MSVC_SHARED_FILENAME_LIB    "libmpg123.lib")
set(EMBEDDED_LIBMPG123_MSVC_SHARED_FILENAME_DLL    "libmpg123.dll")

cmutils_define_os_variables()
if(OS_WINDOWS)
	# Static link option
	option(MAGICPLAYER_STATICALLY_LINK_LIBMPG123 "Statically link libmpg123" OFF)

	# Embeded library config
	cmutils_define_compiler_variables()
	set(COMPILER_FOLDER "mingw")
	if(COMPILER_CLANG OR COMPILER_MSVC)
		set(COMPILER_FOLDER "msvc")
	endif()

	cmutils_define_arch_variables()
	set(ARCH_FOLDER "x86")
	if(ARCH_64BITS)
		set(ARCH_FOLDER "x64")
	endif()

	set(EMBEDED_LIBMPG123_PATH ${CMAKE_CURRENT_SOURCE_DIR}/extlibs/${EMBEDDED_LIBMPG123_NAME}-${EMBEDDED_LIBMPG123_VERSION})
	set(EMBEDED_LIBMPG123_LINK_PATH ${EMBEDED_LIBMPG123_PATH}/lib/${COMPILER_FOLDER}/${ARCH_FOLDER})
	set(EMBEDED_LIBMPG123_COPY_PATH ${EMBEDED_LIBMPG123_PATH}/lib/${COMPILER_FOLDER}/${ARCH_FOLDER})
	if(MAGICPLAYER_STATICALLY_LINK_LIBMPG123)
		# Static link
		set(EMBEDED_LIBMPG123_LIBRARY_TYPE SHARED)
		set(EMBEDED_LIBMPG123_LINK_PATH ${EMBEDED_LIBMPG123_LINK_PATH}/static)
		if(COMPILER_CLANG OR COMPILER_MSVC)
			set(EMBEDED_LIBMPG123_LINK_PATH ${EMBEDED_LIBMPG123_LINK_PATH}/${EMBEDDED_LIBMPG123_MSVC_STATIC_FILENAME})
		else()
			set(EMBEDED_LIBMPG123_LINK_PATH ${EMBEDED_LIBMPG123_LINK_PATH}/${EMBEDDED_LIBMPG123_MINGW_STATIC_FILENAME})
		endif()
	else()
		# Dynamic link
		set(EMBEDED_LIBMPG123_LIBRARY_TYPE STATIC)
		set(EMBEDED_LIBMPG123_LINK_PATH ${EMBEDED_LIBMPG123_LINK_PATH}/shared)
		set(EMBEDED_LIBMPG123_COPY_PATH ${EMBEDED_LIBMPG123_COPY_PATH}/shared)
		if(COMPILER_CLANG OR COMPILER_MSVC)
			set(EMBEDED_LIBMPG123_LINK_PATH ${EMBEDED_LIBMPG123_LINK_PATH}/${EMBEDDED_LIBMPG123_MSVC_SHARED_FILENAME_LIB})
			set(EMBEDED_LIBMPG123_COPY_PATH ${EMBEDED_LIBMPG123_COPY_PATH}/${EMBEDDED_LIBMPG123_MSVC_SHARED_FILENAME_DLL})
		else()
			set(EMBEDED_LIBMPG123_LINK_PATH ${EMBEDED_LIBMPG123_LINK_PATH}/${EMBEDDED_LIBMPG123_MINGW_SHARED_FILENAME})
			set(EMBEDED_LIBMPG123_COPY_PATH ${EMBEDED_LIBMPG123_COPY_PATH}/${EMBEDDED_LIBMPG123_MINGW_SHARED_FILENAME})
		endif()

		# Copy binary to output folder
		configure_file(${EMBEDED_LIBMPG123_COPY_PATH} ${CMAKE_RUNTIME_OUTPUT_DIRECTORY} COPYONLY)
	endif()

	# Declare libmpg123
	add_library(libmpg123 ${EMBEDED_LIBMPG123_LIBRARY_TYPE} IMPORTED)

	# Add includes
	target_include_directories(
		libmpg123 SYSTEM INTERFACE
		"${EMBEDED_LIBMPG123_PATH}/include"
	)

	# Add linked binary
	set_target_properties(
		libmpg123 PROPERTIES
		IMPORTED_LOCATION "${EMBEDED_LIBMPG123_LINK_PATH}"
	)
else()
	# Find PkgConfig
	find_package(PkgConfig REQUIRED)
	if (NOT PKG_CONFIG_FOUND)
		message(FATAL_ERROR "PkgConfig not found (used to find libmpg123)")
	endif()

	# Find libmpg123
	pkg_search_module(LIBMPG123 REQUIRED libmpg123)
	if (NOT LIBMPG123_FOUND)
		message(FATAL_ERROR "libmpg123 not found (dev version must be installed on the system to compile)")
	endif()

	# Declare libmpg123
	add_library(libmpg123 SHARED IMPORTED)

	# Add includes
	target_include_directories(
		libmpg123 SYSTEM INTERFACE
		"${LIBMPG123_INCLUDE_DIRS}"
	)

	# Add linked binary
	set_target_properties(
		libmpg123 PROPERTIES
		IMPORTED_LOCATION "${LIBMPG123_LINK_LIBRARIES}"
	)
endif()

message(STATUS "Configuring libmpg123 - Done")
